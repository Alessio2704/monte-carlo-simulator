// A script is a sequence of directives and assignments in any order.
start: (directive | assignment)*

directive: "@" CNAME "=" expression -> directive_setting
assignment: LET CNAME "=" expression -> assignment

// --- HIERARCHY FOR MATHEMATICAL EXPRESSIONS ---
?expression: term ((ADD|SUB) term)*         -> infix_expression
?term:       factor ((MUL|DIV) factor)*       -> infix_expression
?factor:     power
?power:      atom (POW atom)*            -> infix_expression
?atom: SIGNED_NUMBER
     | CNAME
     | STRING
     | function_call
     | element_access
     | vector
     | "(" expression ")"

// --- Reusable Rule Definitions ---
vector: "[" [arg ("," arg)*] "]"
function_call: CNAME "(" [arg ("," arg)*] ")"
element_access: CNAME "[" expression "]"
?arg: expression

// --- Terminal Definitions ---
LET: "let" /\b/
ADD: "+"
SUB: "-"
MUL: "*"
DIV: "/"
POW: "^"
STRING: /"[^"]*"/

%import common.CNAME
%import common.SIGNED_NUMBER
%ignore COMMENT
COMMENT: /#.*/
%import common.WS
%ignore WS